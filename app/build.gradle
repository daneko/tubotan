plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'org.jmailen.kotlinter'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def keysPropsFile = file("$rootDir/keys.props")
if (!keysPropsFile.exists()) {
    throw new GradleException("create keys.props")
}
def keysProps = new Properties()
keysPropsFile.withInputStream { keysProps.load(it) }

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.github.daneko.tubotan"
        minSdk 22
        // TODO PendingIntentを使ってはいないはずだが、それ関係で死ぬ
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile new File(file(keysProps.getProperty("keys_dir")), "dev.keystore")
            storePassword "krbVTVSq8qhO"
            keyAlias "key_alias"
            keyPassword "krbVTVSq8qhO"
        }
        release {
            storeFile new File(file(keysProps.getProperty("keys_dir")), "product.keystore")
            storePassword "krbVTVSq8qhO"
            keyAlias "key_alias"
            keyPassword "krbVTVSq8qhO"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        // Treat all Kotlin warnings as errors
        allWarningsAsErrors = true
        // Set JVM target to 1.8
        jvmTarget = "1.8"
        // Allow use of @OptIn
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        // Enable default methods in interfaces
        freeCompilerArgs += "-Xjvm-default=all"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
            testLogging {
                showStandardStreams true
                events 'started', 'skipped', 'passed', 'failed'
                exceptionFormat 'full'
            }
        }
    }
}

dependencies {

    // compose
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material3:material3:1.0.0-alpha01"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // accompanist
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"

    // androidx
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation 'androidx.activity:activity-compose:1.4.0'

    // firebase
    implementation platform("com.google.firebase:firebase-bom:29.0.0")
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'

    // ad
    implementation 'com.google.android.gms:play-services-ads:20.4.0'

    implementation 'com.google.android.material:material:1.5.0-beta01'

    implementation "io.arrow-kt:arrow-core:$arrow_version"

    implementation 'com.jakewharton.timber:timber:5.0.1'

    def kotestVersion = '4.6.3'
    testImplementation "io.kotest:kotest-runner-junit5:$kotestVersion"
    testImplementation "io.kotest:kotest-framework-datatest:$kotestVersion"
    testImplementation 'io.kotest.extensions:kotest-extensions-robolectric:0.4.0'
    testImplementation 'androidx.test:core-ktx:1.4.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}
