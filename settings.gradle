pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    plugins {
        def asPluginVersion = '7.1.0-beta03'
        def kotlinVersion = '1.5.31'
        id 'com.android.application' version asPluginVersion
        id 'com.android.library' version asPluginVersion
        id 'org.jetbrains.kotlin.android' version kotlinVersion
        id 'org.jetbrains.kotlin.plugin.parcelize' version kotlinVersion
        id 'org.jmailen.kotlinter' version '3.6.0'
        id 'com.google.gms.google-services' version '4.3.10'
        id 'com.google.firebase.crashlytics' version '2.8.0'
    }
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == 'com.google.gms.google-services') {
                useModule('com.google.gms:google-services:4.3.10')
            }
            if (requested.id.id == 'com.google.firebase.crashlytics') {
                useModule('com.google.firebase:firebase-crashlytics-gradle:2.8.0')
            }
        }
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
}
rootProject.name = "Tubotan"
include ':app'
